#! /usr/bin/env bash

awk '
function output(level, old_level, label, basic, main, advanced, text) {
	bad_char = "?";
	check_char = "✔️";
	emdash = "—";

	if (level == 1) {
		if (label == "?") {
			label = bad_char;
		} else {
			if (basic == "y") {
				basic = sprintf("[%s](#%s-basic)", check_char, label);
			} else if (basic == "n") {
				basic = emdash;
			}
			if (main == "y") {
				main = sprintf("[%s](#%s-main)", check_char, label);
			} else if (main == "n") {
				main = emdash;
			}
			if (advanced == "y") {
				advanced = sprintf("[%s](#%s-advanced)", check_char, label);
			} else if (advanced == "n") {
				advanced = emdash;
			}
		}
		label = sprintf("[%s](#%s)", label, label);
	}

	if (level == 1 && old_level < 1) {
		printf "\n| ID | Unit | Foundational | Main | Advanced |\n";
		printf "|---|----------|---|---|---|\n";
	}
	if (level == 1) {
		printf "| [%s] | %s | %s | %s | %s |\n", label, text, basic, main, advanced;
	}
	if ((level == 0 || level == -1) && old_level > 0) {
		printf "\n\n";
	}
	if (level == 0) {
		printf "\n";
		printf "## %s {#%s}\n", text, label;
	}
}

BEGIN {
	old_level = -1;
}

(NF > 0) {
	line = $0;
	level = 0;
	while (substr(line, level + 1, 1) == "\t") {
		++level;
	}

	label = "";
	basic = "";
	main = "";
	advanced = "";
	text = "";

	if (level == 0) {

		label = $1;
		text = substr($0, index($0, $2));
		output(level, old_level, label, basic, main, advanced, text);

	} else if (level == 1) {

		label = $1;
		basic = $2;
		main = $3;
		advanced = $4;
		text = substr($0, index($0, $5));

		output(level, old_level, label, basic, main, advanced, text);

	}

	old_level = level;

}

END {

	level = -1;

	label = "";
	basic = "";
	main = "";
	advanced = "";
	text = "";

	output(level, old_level, label, basic, main, advanced, text);

}

' -
